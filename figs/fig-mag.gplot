
set terminal pslatex rotate size 3.417,2.111
set multiplot

B = 0.0861
A = 0.094
M0 = 1.21039
func = "figs/fig-mag_data-func.dat"
num = "figs/fig-mag_data-num.dat"

set samples 10000
set key off

set xrange [-20:1200]
set yrange [1.18:1.75]
set ylabel offset 1,0 '$Mt^{-\beta}$'
set xlabel '$ht^{-\Delta}$'

plot num with yerrorbars pt 0, func using (10**$1 / B):(M0 + 10**($2) * A) with linespoints pt 0 lw 3

set size 0.6,0.6
set origin 0.38,0.2
set xrange [-3:3]
set yrange [1.18:1.75]
set ylabel offset 4,0 '\footnotesize$Mt^{-\beta}$'
set xlabel offset 0,0.5 '\footnotesize$ht^{-\Delta}$'
set xtics format '\footnotesize$10^{%g}$' -2,2,3
set ytics format '\footnotesize %g'

plot num using (log10($1)):2:3 with yerrorbars pt 0, func using ($1-log10(B)):(M0 + A * 10**$2) with linespoints pt 0 lw 3



